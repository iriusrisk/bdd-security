import net.masterthought.cucumber.ReportBuilder

apply plugin: 'groovy'
apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'maven'
apply plugin: 'java-library-distribution'
def cucumberVersion = "1.2.4"

defaultTasks 'test'

buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath "org.codehaus.gpars:gpars:1.2.1",
                "net.masterthought:cucumber-reporting:1.4.0"
    }
}

allprojects {
    apply plugin: 'java'
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
}

repositories {
    mavenCentral()
}

configurations {
    cucumberRuntime {
        extendsFrom testRuntime
    }
}

task cleanReports() {
    File reportOutputDirectory = new File("build/reports/cucumber")
    reportOutputDirectory.deleteDir()
}

task cucumber() {
    dependsOn clean, cleanReports, assemble, compileTestJava
    doLast {
        javaexec {
            main = "cucumber.api.cli.Main"
            classpath = configurations.cucumberRuntime + sourceSets.main.output + sourceSets.test.output
            args = ['--plugin', 'pretty', '--glue', 'net.continuumsecurity.steps', 'src/test/resources/features']
        }
        generateReport()
    }
}

task generateReportTask() {
    doLast {
        generateReport()
    }
}

task buildIt() {
    dependsOn assemble, compileTestJava, cleanReports
}

test {
    testLogging {
        exceptionFormat = 'full'
    }
    systemProperties = System.properties
    dependsOn assemble, compileTestJava, cleanReports
    finalizedBy(generateReportTask)
}

//Ensure the cucumber tests are executed as part of the build. Makes for a very pretty output.
build.dependsOn cucumber

def generateReport() {
    File reportOutputDirectory = new File("build/reports/cucumber/pretty")
    reportOutputDirectory.deleteDir()

    def jsonReports = fileTree(dir: "${reporting.baseDir}/cucumber").include '**/*.json'.toString()
    List<String> jsonReportFiles = new ArrayList<String>()
    jsonReports.each { File file ->
        jsonReportFiles.add("${reporting.baseDir}/cucumber/${file.name}".toString());
    }

    String projectName = "BDD-Security";
    boolean skippedFails = false;
    boolean pendingFails = false;
    boolean undefinedFails = true;
    boolean missingFails = true;
    boolean runWithJenkins = false;
    boolean parallelTesting = false;

    net.masterthought.cucumber.Configuration configuration = new net.masterthought.cucumber.Configuration(reportOutputDirectory, projectName);
// optionally only if you need
    configuration.setStatusFlags(skippedFails, pendingFails, undefinedFails, missingFails);
    configuration.setParallelTesting(parallelTesting);
    configuration.setRunWithJenkins(runWithJenkins);
    configuration.setBuildNumber("SecTests.1");
    ReportBuilder reportBuilder = new ReportBuilder(jsonReportFiles, configuration);
    reportBuilder.generateReports();
    println("\nReport available on: "+reportOutputDirectory.getCanonicalPath()+"/feature-overview.html")
}

dependencies {
    compile group: 'net.masterthought', name: 'cucumber-reporting', version: "1.4.0"
    compile 'junit:junit:4.11'
    compile "info.cukes:cucumber-junit:$cucumberVersion"
    compile "info.cukes:cucumber-java:$cucumberVersion"
    compile "info.cukes:cucumber-picocontainer:$cucumberVersion"

    compile 'org.glassfish.jersey.core:jersey-client:2.25.1'
    compile 'com.fasterxml.jackson.jaxrs:jackson-jaxrs-json-provider:2.8.7'
    compile 'org.glassfish.jersey.media:jersey-media-moxy:2.25.1'
    compile 'junit:junit-dep:4.11'
    compile 'org.hamcrest:hamcrest-all:1.3'
    compile ('org.seleniumhq.selenium:selenium-java:3.12.0') { exclude group: 'junit' }
    compile 'org.seleniumhq.selenium:selenium-api:3.12.0'
    compile 'org.seleniumhq.selenium:htmlunit-driver:2.29.0'
    compile 'log4j:log4j:1.2.17'
    compile 'args4j:args4j:2.0.16'
    compile 'commons-configuration:commons-configuration:1.8'
    compile 'uk.com.robust-it:cloning:1.9.0'
    compile 'jline:jline:2.6'
    compile 'com.googlecode.java-diff-utils:diffutils:1.2.1'
    compile 'org.codehaus.jackson:jackson-mapper-asl:1.9.12'
    compile 'commons-jxpath:commons-jxpath:1.3'
    compile 'org.glassfish.jersey.core:jersey-client:2.25.1'
    compile 'org.glassfish.jersey.connectors:jersey-apache-connector:2.25.1'
    compile 'net.htmlparser.jericho:jericho-html:3.3'
    compile 'edu.umass.cs.benchlab:harlib:1.1.2'
    compile group: 'org.zaproxy', name: 'zap-clientapi', version: '1.6.0'
    compile files ("lib/zap-2.8.1.jar", "lib/nessus-java-client-0.2-SNAPSHOT.jar","lib/jssylze-0.2-SNAPSHOT.jar")
}

distZip.shouldRunAfter(build)


jar {
    exclude 'META-INF/*.SF', 'META-INF/*.DSA', 'META-INF/*.RSA', 'META-INF/*.MF'
    manifest {
        attributes 'Class-Path': configurations.compile.files.collect { "lib/$it.name" }.join(' '),
	'Main-Class':'net.continuumsecurity.junit.SecurityTestRunner'
    }
}
